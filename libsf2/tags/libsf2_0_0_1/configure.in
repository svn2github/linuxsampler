AC_INIT(configure.in)

#------------------------------------------------------------------------------------
# Library's "official" release version:

LIBSF2_RELEASE_MAJOR=0
LIBSF2_RELEASE_MINOR=0
LIBSF2_RELEASE_BUILD=1

#------------------------------------------------------------------------------------
# The following is the libtool / shared library version. This doesn't have to
# do anything with the release version. It MUST conform to the following rules:
#
#  1. Start with version information of `0:0:0' for each libtool library.
#  2. Update the version information only immediately before a public release of
#     your software. More frequent updates are unnecessary, and only guarantee
#     that the current interface number gets larger faster.
#  3. If the library source code has changed at all since the last update, then
#     increment revision (`c:r:a' becomes `c:r+1:a').
#  4. If any interfaces have been added, removed, or changed since the last update,
#     increment current, and set revision to 0.
#  5. If any interfaces have been added since the last public release, then increment
#     age.
#  6. If any interfaces have been removed since the last public release, then set age
#     to 0.

LIBSF2_LT_CURRENT=0
LIBSF2_LT_REVISION=0
LIBSF2_LT_AGE=0
SHARED_VERSION_INFO="$LIBSF2_LT_CURRENT:$LIBSF2_LT_REVISION:$LIBSF2_LT_AGE"


AC_LANG_CPLUSPLUS
AC_PROG_CXX
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

AC_C_BIGENDIAN

AC_SUBST(SHLIB_VERSION_ARG)
AC_SUBST(SHARED_VERSION_INFO)
AC_SUBST(LIBSF2_RELEASE_MAJOR)
AC_SUBST(LIBSF2_RELEASE_MINOR)
AC_SUBST(LIBSF2_RELEASE_BUILD)


AC_CHECK_HEADERS(uuid/uuid.h)
AC_SEARCH_LIBS(uuid_generate, uuid)
AC_CHECK_FUNCS(uuid_generate CFUUIDCreate)

case "$host" in
    *mingw*|*cygwin*)
        win32=yes
        ;;
    *)
        win32=no
        ;;
esac
AM_CONDITIONAL(WIN32, test "$win32" = "yes")

if test "$ac_cv_func_uuid_generate" = no -a "$ac_cv_func_CFUUIDCreate" = no -a "$win32" = no; then
   AC_MSG_WARN([No UUID generate function found.
*** libsf2 will not be able to create IDs in sf2 files.
*** Install libuuid from e2fsprogs to solve this. Package might be called
*** uuid-dev, libext2fs2-devel or e2fsprogs-devel.])
fi

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libsf2, "$LIBSF2_RELEASE_MAJOR.$LIBSF2_RELEASE_MINOR.$LIBSF2_RELEASE_BUILD")

AC_OUTPUT( \
    sf2.pc \
    libsf2.spec \
    Makefile \
    src/Makefile \
)
